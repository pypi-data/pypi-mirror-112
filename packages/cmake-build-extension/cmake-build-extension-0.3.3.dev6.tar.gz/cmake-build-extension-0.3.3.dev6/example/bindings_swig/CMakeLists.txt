# Find Python3 and NumPy
find_package(Python3 COMPONENTS Interpreter Development.Module NumPy REQUIRED)

# Set SWIG policies
cmake_policy(SET CMP0078 NEW)
cmake_policy(SET CMP0086 NEW)

find_package(SWIG 4.0 REQUIRED)
set(UseSWIG_MODULE_VERSION 2)
include(${SWIG_USE_FILE})

# =======
# Library
# =======

set(swig_name "bindings")
set_source_files_properties(${swig_name}.i PROPERTIES
    CPLUSPLUS ON
    SWIG_MODULE_NAME "bindings")

swig_add_library(${swig_name}
    TYPE MODULE
    LANGUAGE python
    OUTPUT_DIR ${CMAKE_CURRENT_BINARY_DIR}/mymath
    OUTFILE_DIR ${CMAKE_CURRENT_BINARY_DIR}
    SOURCES ${swig_name}.i)

target_link_libraries(${swig_name}
    PRIVATE mymath Python3::NumPy)

set_target_properties(${swig_name} PROPERTIES
    LIBRARY_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/mymath)

set_property(TARGET ${swig_name} PROPERTY
    SWIG_USE_TARGET_INCLUDE_DIRECTORIES TRUE)

set_property(TARGET ${swig_name}
    PROPERTY SWIG_COMPILE_OPTIONS -doxygen)

set_property(
    TARGET ${swig_name}
    PROPERTY SWIG_DEPENDS numpy.i)

# =======
# Install
# =======

# Handle where to install the resulting Python package
if(CALL_FROM_SETUP_PY)
    # The CMakeExtension will set CMAKE_INSTALL_PREFIX to the root
    # of the resulting sdist / wheel archive
    set(MYMATH_INSTALL_PREFIX ${CMAKE_INSTALL_PREFIX})
else()
    # The Python package is installed directly in the folder of the
    # detected interpreter (system, user, or virtualenv)
    set(MYMATH_INSTALL_PREFIX ${Python3_SITELIB})
endif()

# Get the autogenerated Python file
get_property(WRAPPER_PY_FILE
    TARGET ${swig_name}
    PROPERTY SWIG_SUPPORT_FILES)

# Install the autogenerated Python file
install(
    FILES ${WRAPPER_PY_FILE}
    DESTINATION ${MYMATH_INSTALL_PREFIX})

# Install the SWIG library
install(
    TARGETS ${swig_name}
    LIBRARY DESTINATION ${MYMATH_INSTALL_PREFIX}
    ARCHIVE DESTINATION ${MYMATH_INSTALL_PREFIX}
    RUNTIME DESTINATION ${MYMATH_INSTALL_PREFIX})
